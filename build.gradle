buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.3'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2-rc1'
    }
}

apply plugin: 'org.sonarqube'

allprojects {
    apply plugin: 'com.bmuschko.docker-remote-api'

    docker {
        url = project.hasProperty('dockerHost') ? project.dockerHost : 'https://192.168.99.100:2376'

        if (url.contains("https://")) {
            certPath = project.hasProperty('dockerCertPath') ? project.dockerCertPath : file("${System.getProperty('user.home')}/.docker/machine/machines/default")
        }

        registryCredentials {
            url = project.hasProperty('dockerRegistryUrl') ? project.dockerRegistryUrl : 'qaware-oss-docker-registry.bintray.io'
            username = project.hasProperty('dockerRegistryUsername') ? project.dockerRegistryUsername : 'qaware-oss'
            password = project.hasProperty('dockerRegistryApiKey') ? project.dockerRegistryApiKey : ''
            email = project.hasProperty('dockerRegistryEmail') ? project.dockerRegistryEmail : 'qaware-oss@qaware.de'
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot'
    apply plugin: "com.github.hierynomus.license"

    license {
        skipExistingHeaders = true
        header rootProject.file('LICENSE')
        includes(["**/*.java"])
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    group = 'de.qaware.cloud.nativ.zwitscher'
    version = '1.2.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.RELEASE"
        }
    }

    configurations {
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-undertow'

        compile 'org.springframework.boot:spring-boot-starter-actuator'
        compile 'org.springframework.boot:spring-boot-starter-hateoas'

        compile 'org.projectlombok:lombok:1.16.6'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    springBoot {
        executable = true
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    task buildDockerImage(type: DockerBuildImage) {
        inputDir = projectDir
        noCache = false
        remove = true
        tag = "$project.name:$version"
    }

    task removeDockerImage(type: DockerRemoveImage) {
        imageId = "$project.name:$version"
    }

    task pushDockerImage(type: DockerPushImage) {
        def tag = project.hasProperty("tag") ? project.tag : "$version"
        imageName = "10.81.16.196/qaware-oss/zwitscher/$project.name:$tag"
    }

    task tagDockerImageWithVersion(type: DockerTagImage) {
        imageId = "$project.name:$version"
        repository = "10.81.16.196/qaware-oss/zwitscher/$project.name"
        tag = "$version"
    }

    task tagDockerImageWithLatest(type: DockerTagImage) {
        imageId = "$project.name:$version"
        repository = "10.81.16.196/qaware-oss/zwitscher/$project.name"
        tag = 'latest'
    }

    task TagDockerImage   {
        dependsOn 'tagDockerImageWithLatest'
        dependsOn 'tagDockerImageWithVersion'
    }


}

import com.bmuschko.gradle.docker.tasks.DockerInfo
import com.bmuschko.gradle.docker.tasks.DockerVersion
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

task dockerInfo(type: DockerInfo) {}
task dockerVersion(type: DockerVersion) {}

